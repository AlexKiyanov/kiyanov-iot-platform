import com.github.davidmc24.gradle.plugin.avro.GenerateAvroJavaTask

plugins {
    id 'java'
    id 'org.springframework.boot' version "$springBootVersion"
    id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
    id 'com.github.davidmc24.gradle.plugin.avro' version "$avroPluginVersion"
}

group = 'com.github.alexkiyanov.iotplatform'
version = "$appVersion"
description = 'Events collector service for IoT platform'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://packages.confluent.io/maven/'
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-cassandra'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation "io.confluent:kafka-avro-serializer:$avroSerializerVersion"
    implementation "org.apache.avro:avro:1.11.3"
    implementation("com.github.ben-manes.caffeine:caffeine:$caffeineVersion")
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

avro {
    createSetters = true
    fieldVisibility = 'PRIVATE'
    stringType = 'String'
}

sourceSets {
    main {
        java {
            srcDirs += ['src/main/java']
        }
    }
}

task generateAvroClasses(type: GenerateAvroJavaTask) {
    source = fileTree(dir: "src/main/resources/avro", include: "**/*.avsc")
    outputDir = file("src/main/java")
    fieldVisibility = 'PRIVATE'
    createSetters = true
    stringType = 'String'
}

compileJava.dependsOn generateAvroClasses

clean {
    delete 'src/main/java/com/github/alexkiyanov/iotplatform/ecs/avro'
}

tasks.named('test') {
    useJUnitPlatform()
}
